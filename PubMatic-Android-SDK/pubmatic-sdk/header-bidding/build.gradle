apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.pm-library'

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    jacoco {
        version "0.7.1.201405082137"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-core:1.10.19'
    compile project(path: ':pubmatic-sdk:common-sdk')
    compile project(path: ':pubmatic-sdk:banner-sdk')
    compile 'com.google.android.gms:play-services-ads-lite:9.2.1'
}


// Task to delete the old jar
task deleteOldJar(type: Delete) {
    delete '../SDK-JAR/header-bidding.jar'
}

// Task to export contents as jar
task exportJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('../SDK-JAR/')
    include('classes.jar')
    //Name of Jar
    rename('classes.jar', 'header-bidding.jar')
}

exportJar.dependsOn(deleteOldJar, build)